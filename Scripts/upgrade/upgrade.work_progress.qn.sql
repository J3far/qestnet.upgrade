
-- Ensure QestUUID
IF EXISTS (SELECT 1 FROM INFORMATION_SCHEMA.TABLES WHERE TABLE_NAME = 'WorkProgress')
BEGIN
	IF NOT EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'WorkProgress' AND COLUMN_NAME = 'QestUUID')
	BEGIN 
		ALTER TABLE WorkProgress ADD QestUUID uniqueidentifier NULL 
	END
END
GO

-- Correct any bad or missing QestUUID values
IF EXISTS (SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'WorkProgress' AND COLUMN_NAME = 'QestUUID')
BEGIN
	UPDATE WorkProgress SET QestUUID = R.QestUUID FROM WorkProgress W
	INNER JOIN qestReverseLookup R ON W.QestID = R.QestID AND W.QestUniqueID = R.QestUniqueID
	WHERE W.QestUUID IS NULL OR NOT W.QestUUID = R.QestUUID
END
GO

-- Delete any where the document is gone
IF EXISTS (SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'WorkProgress' AND COLUMN_NAME = 'QestUUID')
BEGIN
	DELETE FROM WorkProgress WHERE NOT QestUUID IN (SELECT QestUUID FROM qestReverseLookup)
END
GO

-- Set QestUUID non-nullable
IF EXISTS(SELECT 1 FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'WorkProgress' AND COLUMN_NAME = 'QestUUID' AND IS_NULLABLE = 'YES')
BEGIN
	ALTER TABLE WorkProgress ALTER COLUMN QestUUID uniqueidentifier NOT NULL 
END
GO
